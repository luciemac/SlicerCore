# We need to normalize paths because CMake on Windows gives us paths with backslashes
# ExternalProject cache files contain `set("some\path")` which is badly interpreted...
find_package(Python3 REQUIRED COMPONENTS Interpreter Development.Module)
cmake_path(CONVERT ${Python3_EXECUTABLE} TO_CMAKE_PATH_LIST Python3_EXECUTABLE NORMALIZE)
cmake_path(CONVERT ${Python3_INCLUDE_DIR} TO_CMAKE_PATH_LIST Python3_INCLUDE_DIR NORMALIZE)

if(UNIX AND NOT APPLE AND CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64")
  # use the right as VTK enforces it
  # This is automatically handled for the package itself, but not for superbuild dependencies
  set(ep_common_cxx_flags "-D_GLIBCXX_USE_CXX11_ABI=0")
endif()

set(proj ${SKBUILD_PROJECT_NAME})
set(SUPERBUILD_TOPLEVEL_PROJECT ${proj})

include(ExternalProject)
include(ExternalProjectDependency)
include(ExternalProjectGenerateProjectDescription)

# These are magic variables required for Superbuild scripts
set(EP_GIT_PROTOCOL "https") # some clones fail with git:
set(Slicer_BUILD_DICOM_SUPPORT "ON") # always build DICOM support
set(Slicer_USE_TBB "ON") # always use tbb

# Global build options
set(CMAKE_CXX_STANDARD "17")
set(CMAKE_CXX_STANDARD_REQUIRED "ON")
set(CMAKE_CXX_EXTENSIONS "OFF")
set(CMAKE_POSITION_INDEPENDENT_CODE "TRUE")

# All deps are install in a single prefix for easier finds
set(EP_DEPENDENCIES_INSTALL_DIR "${CMAKE_BINARY_DIR}/deps-install")
list(APPEND CMAKE_PREFIX_PATH "${EP_DEPENDENCIES_INSTALL_DIR}")

# Set install RPATH of some dependencies
# All dependencies will be installed in the same directory, so $ORIGIN or @loader_path is good enough
set(CMAKE_INSTALL_RPATH)
if(APPLE)
  set(CMAKE_INSTALL_RPATH "@loader_path")
elseif(NOT WIN32)
  set(CMAKE_INSTALL_RPATH "$ORIGIN")
endif()

# Base variables for all platforms and CMake version
mark_as_superbuild(
  VARS
    # Global build options
    BUILD_SHARED_LIBS
    CMAKE_BUILD_TYPE
    CMAKE_CXX_STANDARD
    CMAKE_CXX_STANDARD_REQUIRED
    CMAKE_CXX_EXTENSIONS
    CMAKE_POSITION_INDEPENDENT_CODE
    # forward VTK and helper prefixes
    CMAKE_PREFIX_PATH
    CMAKE_MODULE_PATH
    CMAKE_INSTALL_RPATH
    # Forward main scikit-build core properties
    SKBUILD_STATE
    SKBUILD_PROJECT_NAME
    SKBUILD_PROJECT_VERSION
    SKBUILD_PROJECT_VERSION_FULL
  ALL_PROJECTS
)

# Some dependencies use really old CMake policies,
# anything below 3.5 is no longer supported with CMake 4.
if(CMAKE_VERSION VERSION_GREATER_EQUAL 4.0)
  set(CMAKE_POLICY_VERSION_MINIMUM 3.5)
  mark_as_superbuild(VARS CMAKE_POLICY_VERSION_MINIMUM ALL_PROJECTS)
endif()

if(CMAKE_CXX_COMPILER_FRONTEND_VARIANT STREQUAL "MSVC")
  # Enforce default runtime library with MSVC to MDd for Debug, MD otherwise.
  # CMake tries to use static version by default.
  set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>DLL")
  # Some dependencies use old CMake policies (<3.15),
  # making CMAKE_MSVC_RUNTIME_LIBRARY unused by CMake to set the right runtime with MSVC
  # so we will force the CMAKE_C[XX]_FLAGS to have the /MD or /MDd flag
  foreach(lang IN ITEMS C CXX)
    set("CMAKE_${lang}_FLAGS_DEBUG_INIT" "${CMAKE_${lang}_FLAGS_DEBUG} /MDd")
    set("CMAKE_${lang}_FLAGS_RELEASE_INIT" "${CMAKE_${lang}_FLAGS_RELEASE} /MD")
    set("CMAKE_${lang}_FLAGS_RELWITHDEBINFO_INIT" "${CMAKE_${lang}_FLAGS_RELWITHDEBINFO} /MD")
    set("CMAKE_${lang}_FLAGS_MINSIZEREL_INIT" "${CMAKE_${lang}_FLAGS_MINSIZEREL} /MD")
  endforeach()

  mark_as_superbuild(
    VARS
      CMAKE_CXX_FLAGS_DEBUG_INIT
      CMAKE_CXX_FLAGS_RELEASE_INIT
      CMAKE_CXX_FLAGS_RELWITHDEBINFO_INIT
      CMAKE_CXX_FLAGS_MINSIZEREL_INIT
      CMAKE_MSVC_RUNTIME_LIBRARY
    ALL_PROJECTS
  )
endif()

# Forward used linker if any
if(DEFINED CMAKE_LINKER_TYPE)
  mark_as_superbuild(VARS CMAKE_LINKER_TYPE ALL_PROJECTS)
endif()

if(APPLE)
  mark_as_superbuild(VARS
    CMAKE_OSX_ARCHITECTURES:STRING
    CMAKE_OSX_SYSROOT:PATH
    CMAKE_OSX_DEPLOYMENT_TARGET:STRING
    ALL_PROJECTS
  )
endif()

if(DEFINED EXTERNAL_PROJECT_CMAKE_CACHE_ARGS)
  foreach(varname IN LISTS EXTERNAL_PROJECT_CMAKE_CACHE_ARGS)
    if(DEFINED ${varname})
      mark_as_superbuild(VARS ${varname} ALL_PROJECTS)
    endif()
  endforeach()
endif()

# This is all the dependencies required for the slicer-core wheel
set(dependencies
  curl
  DCMTK
  ITK
  JsonCpp
  LibArchive
  RapidJSON
  SlicerExecutionModel
  tbb
  teem
  vtkAddon
)

message(STATUS "Looking for dependencies in folder ${EXTERNAL_PROJECT_DIR}")
mark_as_superbuild(dependencies:STRING)

ExternalProject_Include_Dependencies(${proj}
  PROJECT_VAR proj
  DEPENDS_VAR dependencies
  SUPERBUILD_VAR USE_SUPERBUILD
)

ExternalProject_Add(${proj}
  ${${proj}_EP_ARGS}
  DEPENDS ${dependencies}
  SOURCE_DIR ${CMAKE_SOURCE_DIR}
  BINARY_DIR ${CMAKE_BINARY_DIR}/inner-build
  CMAKE_CACHE_ARGS
    -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
    -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
    -DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}
    -DCMAKE_PREFIX_PATH:PATH=${CMAKE_PREFIX_PATH}
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DUSE_SUPERBUILD:BOOL=OFF
    -DBUILD_SDK:BOOL=${BUILD_SDK}
    -DSDK_DEPS_DIR:STRING=${EP_DEPENDENCIES_INSTALL_DIR}
    ${EXTERNAL_PROJECT_OPTIONAL_CMAKE_CACHE_ARGS}
  INSTALL_COMMAND ""
)
ExternalProject_AlwaysConfigure(${proj})

# Forward install code from inner build to this project
install(CODE "include(\"${CMAKE_BINARY_DIR}/inner-build/cmake_install.cmake\")")
